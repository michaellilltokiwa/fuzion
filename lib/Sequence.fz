# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature Sequence
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# Sequence -- ancestor for features that can be converted to a 'list' or a
# 'stream'
#
# Sequences are empty, finite or infinite ordered collections of instances
# of a given type.  Sequences may calculate elements lazily on demand.
#
# Sequence is a 'ref' type, i.e., different sub-features may be assigned
# to a field of type 'Sequence'.
#
# Heirs of Sequence must implement either 'as_list' or 'as_stream'.  Failure
# to implement any of these results in an endless recursion when the Sequence
# is used.
#
Sequence(T type) ref is


  # create a list from this Sequence.
  #
  # A list is immutable, so it can be reused and shared between threads.
  # Compared to a stream, a list may require more (heap) allocation.
  #
  # Default implementation uses as_stream. Heirs must redefine at least
  # one of as_list or as_stream.
  #
  as_list list T is as_stream.as_list


  # create a stream of T.
  #
  # A stream contains mutable state, so it cannot be reused or shared
  # between threads.
  #
  # Default implementation uses as_list.  Heirs must redefine at least
  # one of as_list or as_stream.
  #
  as_stream stream T is as_list.as_stream


  # is this Sequence empty?
  #
  is_empty => as_list.is_empty


  # count the number of elements in this Sequence.  Note that this typically
  # runs forever if executed on an endless list
  #
  count
    pre
      analysis: finite  # in practice, we do not always have this information
    => (map_sequence i32 (_ -> 1)).fold i32.type.sum


  # get the first element of this Sequence
  #
  # Sequence must not be empty, causes precondition failure if debug is enabled.
  #
  first
    pre
      debug: !is_empty
    => as_list.head.get


  # get the first element of this Sequence or default if sequence is empty
  #
  first(default T)
    =>
      if is_empty then default else first


  # get the last element of this Sequence
  #
  # Sequence must not be empty, causes precondition failure if debug is enabled.
  #
  # This may take time in O(count), in particular, it may not terminate
  # for an infinite Sequence.
  #
  last
    pre
      debug: !is_empty
   => as_list.last


  # get the last element of this Sequence or default if sequence is empty
  #
  last(default T)
    =>
    if is_empty then default else last


  # collect the contents of this Sequence into an array
  #
  as_array array T is
    s := as_stream
    (array T).type.new count i->s.next


  # create a stream and call 'for_each f' on it
  #
  for_each(f T -> unit) unit is as_stream.for_each f


  # create a stream and have it consumed by f, infix operator synonym of for_each.
  #
  infix | (f T -> unit) => for_each f


  # create a stream, postfix operator synonym for as_stream
  #
  postfix | => as_stream


  # create a new stream and apply 'f' to each element 'e' as long as 'f e'
  #
  for_while(f T -> bool) unit is as_stream.for_while f


  # create a new stream that contains the first elements of this stream for
  # which 'f e' is false
  #
  before(f T -> bool) stream T is as_stream.before f


  # create a new stream and filter its elements using predicate f
  # values for which f is false are dropped
  #
  filter   (f T -> bool) Sequence T is as_list.filter f


  # create a new stream and filter its elements using predicate f, infix operator
  # synonym of filter.
  #
  infix |& (f T -> bool) => filter f


  # create a new stream and filter its elements using predicate f, infix operator
  # synonym of filter.
  #
  # NYI: What is better, 'infix |&' or 'infix &', or something else?
  #
  infix & (f T -> bool) => filter f


  # create a stream and check if predicate f holds for all elements produced
  #
  infix ∀ (f T -> bool) bool is as_stream ∀ f


  # create a stream and check if predicate f holds for at least one element produced
  #
  infix ∃ (f T -> bool) bool is as_stream ∃ f


  # create a list that consists only of the first n elements of this
  # Sequence, fewer if this stream has fewer elements
  #
  take (n i32) => as_list.take n


  # create a list that consists of the elements of this Sequence except the first
  # n elements
  #
  drop (n i32) => as_list.drop n


  # get a function that, given an index, returns the element at that index
  #
  index [] () i32 -> T is n -> Sequence.this[n]


  # create a slice from this Sequence that consists of the elements starting at index
  # from (including) up to index to (excluding).
  #
  slice(from, to i32) => drop(from).take to-from
    # NYI: OPTIMIZATION: We could redefine this, e.g. to avoid copying array data
    # on array.slice(from,to).as_array.


  # create a tuple of two Sequences by splitting this at the given index, i.e.,
  # a Sequence of length 'at' and one of length 'count-at'.
  #
  # at may be <= 0 or >= count, in which case the resulting tuple will be the
  # (empty list, Sequence.this.as_list) or (Sequence.this.as_list, empty list), resp.
  #
  split_at(at i32) => ((take at), (drop at))

  # Lazily take the first elements of this Sequence for which predicate 'p' holds.
  #
  take_while (p T -> bool) => as_list.take_while p


  # Lazily drop the first elements of this Sequence for which predicate 'p' holds.
  #
  drop_while (p T -> bool) => as_list.drop_while p


  # create a Sequence that consists of all the elements of this Sequence followed
  # by all the elements of s
  #
  concat_sequences (s Sequence T) Sequence T is as_list.concat s.as_list


  # infix operand synonym for concat_sequences
  #
  infix ++ (s Sequence T) => as_list ++ s.as_list


  # create a list that repeats the current Sequence indefinitely.  In case 'Sequence.this'
  # is empty, returns 'nil'
  #
  cycle list T is as_list.cycle


  # create a lazy list of all the tails of this Sequence, including the complete Sequence
  # as a list and the empty list 'nil'.
  #
  tails list (list T) is as_list.tails


  # create a string representation of this Sequence including all the string
  # representations of its contents, separated by ',' and enclosed in '['
  # and ']'.
  #
  redef as_string => as_list.as_string


  # create a string representation of this Sequence including all the string
  # representations of its contents, separated by 'sep'.
  #
  as_string (sep String) => as_list.as_string sep


  # call 'as_string' on the elements
  #
  as_strings => map_sequence c->c.as_string


  # map the Sequence to a new list applying function f to all elements
  #
  # This performs a lazy mapping, f is called only when the elements
  # in the resulting list are accessed.
  #
  # NYI: As long as covariance for result type is not permitted we cannot
  # call this 'map' since this would clash with, e.g., 'array.map'
  #
  map_sequence(B type, f T -> B) => as_list.map f


  # NYI "error 1: Cannot redefine feature with generic arguments
  #      To solve this, ask the Fuzion team to remove this restriction :-)"
  #
  # flat_map(B type, f T -> Sequence B) =>
  #   as_list.flat_map f
  flat_map_sequence(B type, f T -> Sequence B) =>
    as_list.flat_map f


  # fold the elements of this Sequence using the given monoid.
  #
  # e.g., to sum the elements of a stream of i32, use s.fold i32.sum
  #
  fold (m Monoid T) => as_list.fold m.e m


  # reduce this Sequence to R with an initial value init
  # and a reducing function f.
  # the reduction is finished once f yields abort or
  # if the end of the sequence is reached.
  #
  reduce(R type, init R, f (R,T) -> R | abort R) R is
    match as_list
      nil => init
      c Cons =>
        match f init c.head
          a abort => a.val
          r R => c.tail.reduce r f


  # reduce this Sequence to `outcome R`
  # with an initial value `init` and a reducing function `f`.
  # the reduction is finished once `f` yields `abort` or
  # if the end of the sequence is reached.
  #
  reduce_or_error(R type, init R, f (R,T) -> R | abort (outcome R)) outcome R is
    match as_list
      nil => init
      c Cons =>
        match f init c.head
          a abort => a.val
          r R => c.tail.reduce_or_error r f


  # insert element v at position at
  #
  insert(at i32, v T)
   pre
     debug: at ≥ 0
   =>
    take at ++ [v] ++ drop at


  # sort this Sequence using the total order defined by less_or_equal
  sort(less_or_equal (T, T) -> bool) => container.sorted_array Sequence.this less_or_equal


  # sort this Sequence using total order defined for 'f a[i]'
  sort_by(O type : has_total_order, f T->O) => sort (a, b -> f a ≤ f b)


  # create a new list from the result of applying 'f' to the
  # elements of this Sequence and 'b' in order.
  #
  zip(U,V type, b Sequence U, f (T,U)->V) => as_list.zip0 b.as_list f


  # takes a transducer xf, a reducer f and an initial value
  # returns the result of applying the reducer xf f to the Sequence
  transduce(TA,U type, xf transducer TA U T, rf (TA,U) -> TA, init TA) TA is
    red := xf.call rf
    for
      res := init, red.call res el
      el in Sequence.this do
    else
      res


  # apply transducer to sequence, returning a sequence of results
  #
  # example usage:
  # human(age i32) is
  # ages := map (Sequence i32) human i32 (x -> x.age)
  # gt_ten := filter (Sequence i32) i32 (x -> x > 10)
  # xf := ages ∘ gt_ten
  # say ([human(4), human(12), human(30)].into xf) # [12,30]
  into(TA type, xf transducer (Sequence TA) TA T) Sequence TA is
    red := xf.call ((res,val) -> res ++ [val])
    for
      res (Sequence TA) := (Sequence TA).type.empty , red.call res el
      el in Sequence.this do
    else
      res


  # the nth element in the sequence if it exists, wrapped in an option,
  # nil otherwise.
  #
  nth(n i32) option T
    pre
      debug: n ≥ 0
  is
    match drop n
      nil => nil
      c Cons => c.head


  # is this sequence known to be finite?  For infinite sequences, features like
  # count diverge.
  #
  finite => false


  # the nth element in the sequence, must exist
  #
  index [] (i i32) T
    pre
      debug: 0 ≤ i,
      debug: finite: i < count,
      debug: !finite: !(drop i-1).is_empty
  is
    (nth i).get


  # adds the corresponding index to
  # every element in the sequence
  #
  indexed list (tuple i32 T) is
    indexed 0


  # adds an index to every element
  # in the sequence starting at start_idx
  #
  indexed(I type : has_interval I, start_idx I) list (tuple I T) is
    zip (start_idx..) (a,b -> (b, a))



  # group the elements of this sequence by a key of type K
  #
  # f determines the key of an element
  #
  group_by(K type : has_hash, f T -> K) container.Map K (Sequence T) is
    # NYI It should be possible to choose the map implementation that is used.
    res := (container.C_Trie K (Sequence T)).type.empty
    for_each (x ->
      key := f x
      match res[key]
        nil => res.add key [x]
        s Sequence T => res.add key (s ++ [x])
      )
    res



  # chop this Sequence into chunks of `chunk_size`.
  # the last chunk may be smaller than `chunk_size`.
  #
  chunk(chunk_size i32) list (list T)
  pre chunk_size > 0
  is
    if is_empty
      nil
    else
      (take chunk_size).as_list : ((drop chunk_size).chunk chunk_size)



  # create an empty list and return it as a Sequence
  #
  type.empty list T is
    (list T).type.empty


  # monoid of Sequences with infix concatentation operation.
  #
  type.concat_monoid : Monoid (Sequence T) is

    # associative operation
    #
    infix ∙ (a, b Sequence T) Sequence T is a.concat_sequences b

    # identity element
    #
    e Sequence T is
      (Sequence T).type.empty


# Sequences -- unit type defining features related to Sequence but not requiring
# an instance
#
Sequences is


  # determine the index of element x within Sequence l.  0 if x is at the head
  # of the list, 1 if it comes directly after head, etc. nil if x is not
  # in the list.
  #
  index_in(A type : equatable, l Sequence A, x A) => (container.searchable_sequence A l).index_of x


  # get the index of x within this Sequence or nil if it does not exist
  #
  find(A type : equatable, l Sequence A, x Sequence A) => (container.searchable_sequence A l).find x
