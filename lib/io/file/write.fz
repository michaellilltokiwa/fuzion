# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature write
#
#  Author: Wael Youssfi (wael.youssfi@tokiwa.software)
#
# -----------------------------------------------------------------------

# write -- effect providing writing operations for files
#
public write(fwh Write_Handler) : simple_effect is


  # writes the content of an array of bytes to a file opened as fd
  #
  # this might overwrite parts or all of an existing file.
  #
  public write(content array u8) =>
    replace
    fwh.write content


  # the default file writing handler writing bytes into files via fuzion.sys.fileio.write
  #
  module type.default_write_handler(fd i64) : io.file.Write_Handler is
    public write(content array u8) =>
      fuzion.sys.fileio.write fd content
        .bind unit r->
          if fuzion.sys.fileio.flush fd = 0 then r else error "flushing failed"


# short-hand for accessing write effect in current environment
#
public write =>
  write.env


# reference to the writing operations that could take place
#
public Write_Handler ref is
  public write(content array u8) outcome unit => abstract



# write String `s` to file `f`
#
public write(f String, s String) outcome unit =>
  use unit f mode.write ()->
    io.file.open.write s.utf8
