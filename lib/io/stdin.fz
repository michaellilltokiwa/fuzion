# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature stdin
#
#  Author: Michael Lill (michael.lill@tokiwa.software)
#
# -----------------------------------------------------------------------


# stdin -- effect providing several features
# for consuming the given Byte_Input_Handler
#
private stdin(
    ip Byte_Input_Handler,
    # action to be taken: plain nomad, install or replace?
    redef r effectMode.val,
    _ unit
  ) : stdins, effect r is


  # read a single byte from stdin
  #
  byte =>
    (stdin ip.next mode unit).ip.get


  # an infinite list of either a byte, an end_of_file or an error
  # Caution!: this list is not immutable since it is constructed lazily by reading from stdin!
  # you may iterate only once.
  # Additionally there may be read more from stdin than what is consumed
  # via this list.
  # NYI: How can we lessen these imperfections?
  bytes list (choice u8 end_of_file error) is
    list byte ()->bytes


  # read one codepoint from stdin
  # returns end_of_file if stdin is depleted or
  # error in case of an error while reading the codepoint
  #
  codepoint => codepoint0


  # a - possibly infinite - list of codepoints read from stdin
  # Caution!: this list is not immutable since it is constructed lazily by reading from stdin!
  # you may iterate only once.
  # Additionally there may be read more from stdin than what is consumed
  # via this list.
  # NYI: How can we lessen these imperfections?
  codepoints list (choice codepoint end_of_file error) is
    list codepoint0 ()->codepoints


  # read n codepoints from stdin or
  # less if an end_of_file is encountered
  # while reading
  #
  read (n i32) => read0 n


  # read from stdin until end of line or end of file
  #
  read_line => read_line0



private read_line0 outcome String : character_encodings is
  stdin.codepoints
    .reduce_or_error "" (r, t ->
      match t
        c codepoint =>
          if c.val = ascii.lf.as_u32 || c.val = ascii.cr.as_u32
            if r.isEmpty # we read nothing yet, so we just skip the lf/cr
              r
            else
              # NYI explicit tagging should not be necessary #671
              tmp outcome String := r
              abort tmp
          else
            r + c
        end_of_file =>
          # NYI explicit tagging should not be necessary #671
          tmp outcome String := r
          abort tmp
        e error =>
          # NYI explicit tagging should not be necessary #671
          tmp outcome String := e
          abort tmp
    )


# read n codepoints from stdin
# may return less than n bytes if end of file is reached
# or error if stdin can not be read right now
private read0(n i32) outcome String
pre n >= 0
is
  (1..n)
    .reduce_or_error "" (r, t ->
      match codepoint0
        c codepoint =>
          r + c
        end_of_file =>
          # NYI explicit tagging should not be necessary #671
          tmp outcome String := r
          abort tmp // return what we read so far
        e error =>
          # NYI explicit tagging should not be necessary #671
          o outcome String := e
          abort o   // return only the error, discarding what we read so far
      )


# try reading a codepoint from stdin
private codepoint0 choice codepoint end_of_file error is
  match stdin.byte
    b u8 =>
      match codepoint.type.num_utf8_bytes b
        n i32 =>
          rest := (2..n)
            # NYI use reduce_or_error once outcome is open generic
            .reduce (choice (Sequence u8) end_of_file error) [b] (r,t ->
              match r
                seq Sequence u8 =>
                  match stdin.byte
                    b u8 => seq ++ [b]
                    end_of_file =>
                      # NYI explicit tagging should not be necessary #671
                      tmp choice (Sequence u8) end_of_file error := end_of_file
                      abort tmp
                    e error =>
                      # NYI explicit tagging should not be necessary #671
                      tmp choice (Sequence u8) end_of_file error := e
                      abort tmp
                * => fuzion.std.panic "should not be possible"
              )
          match rest
            l Sequence u8 => String.type.fromBytes l
                                    .asCodepoints
                                    .first
            end_of_file => end_of_file
            e error => e // error occurred while reading the rest of utf8 bytes
        e error => e     // first byte is not the start of a utf8 codepoint
    end_of_file => end_of_file
    e error => e         // not even single byte could be read from stdin



# type to indicate an `end of file`
end_of_file is


# short-hand for creating and installing stdin effect
stdin(h Byte_Input_Handler, f () -> unit) =>
  s := stdin h effectMode.new unit
  s.use0 f
  unit

# short-hand for accessing stdin effect in current environment
stdin =>
  stdins.installDefault
  stdin.env


# unit feature providing static helpers
stdins is
  installDefault is
    if !effects.exists io.stdin
      s := stdin (defaultInputHandler (error "not initialized")) effectMode.new unit
      s.default


Byte_Input_Handler ref is

  next Byte_Input_Handler is abstract

  # NYI using choice, until open generic for error in outcome is implemented
  get choice u8 end_of_file error is abstract


# the default input handler reading bytes via fuzion.sys.stdin.nextByte
defaultInputHandler(r choice u8 end_of_file error) : Byte_Input_Handler is

  next Byte_Input_Handler is
    v := fuzion.sys.stdin.next_byte
    if v = -1
      defaultInputHandler end_of_file
    else if v < -1
      defaultInputHandler (error "an error occurred while reading stdin")
    else
      defaultInputHandler v.as_u8

  get => r
